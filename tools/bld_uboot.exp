#
# (C) Copyright 2006
# Wolfgang Denk, DENX Software Engineering, wd@denx.de
#
#
# implementation of bld_ methods for building U-Boot image
#

#
# checks U-Boot tree: takes a peek in the main Makefile for familiar strings
# returns 0/1 if doesn't/match
#
proc check_uboot_tree {} {

	global img_src

	set rv 1
	set cmd "grep"
	set cmd_arg "-r \"U_BOOT_VERSION =\" $img_src/Makefile"

	if [catch {set o [eval exec $cmd $cmd_arg]}] {
		p_verb "U_BOOT_VERSION string not found"
        	set rv 0
	} else {
		p_verb "U-Boot source tree recognized, OK"
	}

	return $rv
}

proc make_mrproper {sid p {to 180}} {
	
	set spawn_id $sid
	set rv 1

	set timeout $to
	set c "make mrproper"
	send -s "$c\r"
	#
	# error strings:
	# make: *** No rule to make target `dfdsf'.  Stop.
	#
	expect {
		timeout {
			p_err "timed out after '$c' command"
			set rv 0
		}
		-re "No\\ rule\\ to\\ make\\ target.*Stop" {
			p_err "no target"
			set rv 0
		}
		-re ".*$p$" {
			p_verb "command '$c' OK"
		}
	}

	return $rv
}

#
# validates environment for U-Boot build:
#
# 1. check tools
#   - path makes sense, gcc, ld etc. exist?
#
# 2. check src dir
#   - exists, writable?
#   - looks like a U-Boot tree?
#
# 3. check dst
#   - if dir: exists, writable?
#   - if endfile: already exists?
#
# 4. check config file
#   - exists include/config.h
#
proc bld_valid_env {} {
	
	global img_name img_src img_dst
	global build_tools build_arch build_cross
	global overwrite no_clean
	global env

	## 1. check tools

	##
	## set env vars: ARCH, CROSS_COMPILE
	##
	set env(ARCH) $build_arch
	set env(CROSS_COMPILE) $build_cross

	##
	## set PATH so the desired toolchain is available
	##
	if ![set_host_tool_path $build_tools] {
        	p_err "path to the build tools not accessible: '$p'?!"
		return 0
	}

	##
	## check tools availability:
	##  - a selected tool from cross build suite, let's have a look at 
	##    gcc and ld
	##
	set cmd_gcc $build_cross
	append cmd_gcc "gcc"
	set cmd_ld $build_cross
	append cmd_ld "ld"

	set tools [list $cmd_gcc $cmd_ld]
	if ![check_host_tools tools] {
		return 0
	}
	p_verb "all tools in place, proceeding"


	## 2. check src dir

	##
	## validate source dir
	##
	##  - exists, writable?
	##  - looks like a U-Boot tree?
	##

	# strip trailing slash
	set img_src [string trimright $img_src "/"]
	if ![valid_dir $img_src 1] {
		p_err "Problems accessing source dir?!"
		return 0
	}

	if ![check_uboot_tree] {
		p_err "Doesn't look like U-Boot source tree: '$img_src'?!\
		Check your -s value."
		return 0
	}
	p_verb "working with source dir: '$img_src'"

	## 3. check dst

	##
	## validate  destination location
	## - if dir: exists, writable?
	## - if endfile: already exists?
	##
	set o [expr {($overwrite == "yes") ? 1 : 0}]
	set img_dst [string trimright $img_dst "/"]
	if ![check_dst $img_dst $o "u-boot.bin"] {
		return 0
	}
	
	## 4. check U-Boot config file 
	##
	## - exists include/configs/<name>.h

	# extract only the core config name if supplied in <name>_config
	# fashion
	regexp {(.+)(_.*config)} $img_name in1 in2 in3]
	if [info exists in1] {
		set img_name $in2
	}
	set c $img_name
	append c ".h"
	set cf "$img_src/include/configs/$c"
	if {([file exists $cf]) && ([file readable $cf])} {
		p_verb "found config file '$cf'"
	} else {
		p_err "no U-Boot config file: '$cf'?! Check your -n value."
		return 0
	}
	p_verb "using U-Boot config: '$img_name'"
	
	##
	## verify if we can skip configuration steps
	##
	if {$no_clean == "yes"} {
		if [file exists $img_src/include/config.h] {
			p_verb "performing 'no-clean' build, config steps will\
			be skipped"
		} else {
			p_verb "'no-clean' build specified but no\
			include/config.h file found so doing full build\
			anyway..."
			set no_clean "no"
		}
	}

	return 1
}

proc bld_pre {} {
	global build_sid build_prompt
	
	p_banner "make mrproper" #
	return [make_mrproper $build_sid $build_prompt]
}

proc bld_config {{to 180}} {

	global build_sid build_prompt img_name
	set spawn_id $build_sid
	set p $build_prompt
	set timeout $to
	set rv 1

	set c "make $img_name"
	append c "_config"
	p_banner $c #
	send -s "$c\r"
	#
	# closing strings:
	# Configuring for BAB7xx board...
	#
	# error strings:
	# make: *** No rule to make target `dfdsf'.  Stop.
	#
	expect {
		timeout {
			p_err "timed out after '$c' command"
			set rv 0
		}
		-re "No\\ rule\\ to\\ make\\ target.*Stop" {
			p_err "no target"
			set rv 0
		}
		-re "Configuring\\ for\\ $img_name\\ board.*$p$" {
			p_verb "command '$c' OK"
		}
	}

	return $rv
}

proc bld_image {{to 600}} {

	global build_sid build_prompt img_name
	set spawn_id $build_sid
	set p $build_prompt
	set timeout $to
	set rv 1
	
	set c "make all"
	p_banner $c #
	send -s "$c\r"
	#
	# closing strings:
	# ppc_6xx-objcopy --gap-fill=0xff -O srec u-boot u-boot.srec
	# ppc_6xx-objcopy --gap-fill=0xff -O binary u-boot u-boot.bin
	#
	# error strings:
	# make: *** No rule to make target `dfdsf'.  Stop.
	# host/ehci-q.c:185: error: invalid storage class for func
	# make[3]: *** [host/ehci-hcd.o] Error 1
	#
	# make[2]: *** [arch/powerpc/kernel/vdso32/gettimeofday.o] Error 1
	# make[1]: *** [arch/powerpc/kernel/vdso32] Error 2
	# make: *** [arch/powerpc/kernel] Error 2
	#
	expect {
		timeout {
			p_err "timed out after '$c' command"
			set rv 0
		}
		-re "No\\ rule\\ to\\ make\\ target.*Stop" {
			p_err "no target"
			set rv 0
		}
		-re ".*:\\ error:.*" {
			send_user -- "\n"
			p_err "build failed"
			set rv 0
		}
		-re ".*make.+\\ Error\\ .*" {
			send_user -- "\n"
			p_err "build failed"
			set rv 0
		}
		-re ".*objcopy.*binary\\ u-boot\\ u-boot\\.bin.*$p$" {
			p_verb "command '$c' OK"
		}
	}

	return $rv
}

proc bld_post {} {

	global img_dst img_src build_sid

	##
	## copy image to the desired location
	##
	p_banner "copy image to destination" *
	set img_file "$img_src/u-boot.bin"
	if {$img_dst == ""} {
		p_verb "no destination location specified, nothing to do.."
	} else {
		send_user -- "Copying image to '$img_dst'\n"
		if ![host_copy $img_file $img_dst] {
			return 0
		}
		p_verb "copied '$img_file' to '$img_dst'"
	}
	return 1
}
